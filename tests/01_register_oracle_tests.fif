#!/usr/bin/env fift -s
"TonUtil.fif" include
"Asm.fif" include

variable updated_storage
variable amount
variable idx
variable int_msg_body
variable int_message

{ { ."success " cr } { "test failed" abort } cond } : test_ret_value
{ swap 0= test_ret_value } : test_code
{ = test_ret_value } : test_code_fail
{ { ."success " cr } { " contract state error: " abort } cond } : test_state
100000 constant timeout
true constant bounce
"build/new-register" constant file-base
file-base +".addr" load-address 2constant dest_addr

"build/register-code.fif" include <s constant code
<b 
    dictnew dict,
    dictnew dict,
    dictnew dict,
    0 Gram,
b> constant initial_storage
"build/register-oracle.boc" file>B B>boc int_msg_body ! 

// [ magic:0x076ef1ea actions:Integer msgs_sent:Integer
// unixtime:Integer block_lt:Integer trans_lt:Integer
// rand_seed:Integer balance_remaining:[Integer (Maybe Cell)]
// myself:MsgAddressInt global_config:(Maybe Cell)
// ] = SmartContractInfo;

0 tuple 0x076ef1ea ,
0 , 0 , 
1570470873 ,
1 , 1 , 0 , 
0 tuple 9223372036854775807 , dictnew , , 
dest_addr nip , dictnew , 
1 tuple 
constant c7

// test # 0 register oracle 

5000 amount !
1 idx !
<b b{00} s, bounce 1 i, b{0100} s, -1 <b idx @ 32 u, b> hashu addr, b{100} s, dest_addr addr, amount @ Gram, 0 9 64 32 + + 1+ u, 
int_msg_body @ <s 2dup s-fits? not rot over 1 i, -rot { drop int_msg_body @ ref, } { s, } cond b>  int_message !

amount @ int_message @ int_msg_body @ <s recv_internal code initial_storage c7 runvmctx
test_code
dup 
updated_storage !
boc>B initial_storage boc>B B= 0= test_state

// test # 1 try to register oracle in second time

amount @ int_message @ int_msg_body @ <s recv_internal code updated_storage @ c7 runvmctx
swap 54 test_code_fail
boc>B updated_storage @ boc>B B= test_state

// test # 2 try to register oracle with insufficient amount
300 amount !
amount @ int_message @ int_msg_body @ <s recv_internal code initial_storage c7 runvmctx
swap 50 test_code_fail
boc>B initial_storage boc>B B= test_state